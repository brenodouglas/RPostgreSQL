# Process this file with autoconf to produce a configure script.
#
# Configure.in for RPostgreSQL
# Copyright (C) 2008 Dirk Eddelbuettel and licensed under GNU GPL
#
# This file draws heavily on other configure.in files from littler (Eddelbuettel and Horner), RMySQL

# Set the name and version -- the version set here will propagate to other files from here
AC_INIT(RPostgreSQL, 0.1)

# Checks for common programs using default macros
AC_PROG_CC

# Check for non-standard programs: pg_configg(1) to configure PostgreSQL builds
AC_PATH_PROG([PG_CONFIG], [pg_config])

# Test for error in preceding check and issue loud complaint if need be
if test "${PG_CONFIG}" = ""; then
   echo
   echo "Error: pg_config not found."
   echo "We cannot install RPostgreSQL without a PostgreSQL installation."
   echo
   AC_MSG_ERROR( aborting! )
fi

# Use pg_config for header and linker arguments
PG_INCDIR=`${PG_CONFIG} --includedir`
PG_LIBDIR=`${PG_CONFIG} --libdir`

# Expand into arguments
PKG_CPPFLAGS="-I${PG_INCDIR}"
PKG_LIBS="-L${PG_LIBDIR} -lpq"

# Test for sanity by looking for libpq-fe.h, no explicit action on found, error on failure
AC_CHECK_FILE(["${PG_INCDIR}/libpq-fe.h"],
	,
	AC_MSG_ERROR([File libpq-fe.h not in ${PG_INCDIR}; installation may be broken.]))

# Test for sanity by looking for libpq.so, no explicit action on found, error on failure
AC_CHECK_FILE(["${PG_LIBDIR}/libpq.so"],
	,
	AC_MSG_ERROR([Library libpq.so not in ${PG_LIBDIR}; installation may be broken.]))

# Now substitute these two variable in src/Makevars.in to create src/Makevars
AC_SUBST(PKG_CPPFLAGS)
AC_SUBST(PKG_LIBS)
AC_OUTPUT(src/Makevars)
